// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "31.0.0"
        minSdkVersion = 21
        compileSdkVersion = 33
        targetSdkVersion = 31
        if (System.properties['os.arch'] == "aarch64") {
            // For M1 Users we need to use the NDK 24 which added support for aarch64
            ndkVersion = "24.0.8215888"
        } else {
            // Otherwise we default to the side-by-side NDK version from AGP.
            ndkVersion = "21.4.7075529"
        }
        googlePlayServicesVersion = "18.1.0"
        googlePlayServicesVisionVersion = "17.0.2"
        firebaseVersion = "17.2.1"
        timberVersion = "4.7.1"
        kotlinVersion = "1.6.10"
        androidXCore = "1.0.2"
        googlePlayServicesLocationVersion = "21.0.1"
        appCompatVersion = "1.4.2"
        mercadoPagoPxVersion = "4.53.2"
        reactNativeVersion = "0.70.6" // https://github.com/expo/expo/issues/18129
    }
    repositories {
        // Gradle 4.1 and higher include support for Google's Maven repo using
        // the google() method. And you need to include this repo to download
        // Android plugin 3.0.0 or higher.
        google()
        jcenter()
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath("com.android.tools.build:gradle:7.3.1")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("de.undercouch:gradle-download-task:5.0.1")
        classpath("com.google.gms:google-services:4.3.8")
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        maven {
            url "$rootDir/../node_modules/detox/Detox-android"
        }
        maven {
            // react-native-background-fetch
            url("${project(':react-native-background-fetch').projectDir}/libs")
        }
        if (findProject(':react-native-background-geolocation') != null) {
          maven {
              // Required for react-native-background-geolocation
              url("${project(':react-native-background-geolocation').projectDir}/libs")
          }
        }
        google()
        // Add jitpack repository (added by tipsi-stripe)
        maven { url 'https://jitpack.io' }
        maven {
          url 'https://artifacts.mercadolibre.com/repository/android-releases/'
        }
        jcenter()
        maven {
            // expo-camera bundles a custom com.google.android:cameraview
            url "$rootDir/../node_modules/expo-camera/android/maven"
        }
    }
}

subprojects {
    afterEvaluate {project ->
        if (project.hasProperty("android")) {
            android {
                buildToolsVersion rootProject.ext.buildToolsVersion
                compileSdkVersion rootProject.ext.compileSdkVersion
            }
        }
    }
}
