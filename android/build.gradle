// Top-level build file where you can add configuration options common to all sub-projects/modules.

import java.util.regex.Matcher
import java.util.regex.Pattern


def buildWithRNBGeolocation() {
    Gradle gradle = getGradle()
    String tskReqStr = gradle.getStartParameter().getTaskRequests().toString()
    Pattern pattern = Pattern.compile("(bundle|assemble|generate)(\\w+)(Release|Debug)")
    Matcher matcher = pattern.matcher(tskReqStr)

    if(matcher.find()) {
      return !matcher.group(2).toLowerCase().equals("client")
    }

    return false
}

project.ext.set("buildWithRNBGeolocation", buildWithRNBGeolocation())

buildscript {
    ext {
        buildToolsVersion = "30.0.2"
        minSdkVersion = 21
        compileSdkVersion = 30
        targetSdkVersion = 30
        ndkVersion = "21.4.7075529"
        supportLibVersion = "28.0.0"
        playServicesVersion = "17.0.0"
        googlePlayServicesVersion = playServicesVersion
        googlePlayServicesVisionVersion = "17.0.2"
        firebaseVersion = "17.2.1"
        timberVersion = "4.7.1"
        kotlinVersion = "1.5.30"
        androidXCore = "1.0.2"
        googlePlayServicesLocationVersion = "17.1.0"
        appCompatVersion = "1.2.0"
        mercadoPagoPxVersion = "4.53.2"
    }
    repositories {
        // Gradle 4.1 and higher include support for Google's Maven repo using
        // the google() method. And you need to include this repo to download
        // Android plugin 3.0.0 or higher.
        google()
        jcenter()
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath("com.android.tools.build:gradle:4.2.2")
        classpath("com.google.gms:google-services:4.3.8")
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        maven {
            url "$rootDir/../node_modules/detox/Detox-android"
        }
        maven {
            // react-native-background-fetch
            url("${project(':react-native-background-fetch').projectDir}/libs")
        }
        if (project.buildWithRNBGeolocation) {
          maven {
              // Required for react-native-background-geolocation
              url("${project(':react-native-background-geolocation').projectDir}/libs")
          }
        }
        google()
        // Add jitpack repository (added by tipsi-stripe)
        maven { url 'https://jitpack.io' }
        maven {
          url 'https://artifacts.mercadolibre.com/repository/android-releases/'
        }
        jcenter()
    }
}

subprojects {
    afterEvaluate {project ->
        if (project.hasProperty("android")) {
            android {
                buildToolsVersion rootProject.ext.buildToolsVersion
                compileSdkVersion rootProject.ext.compileSdkVersion
            }
        }
    }
}
